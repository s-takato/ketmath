use("KetCindyPlugin");
Dircdy=loaddirectory;
setdirectory(gethome());
import("ketcindy.ini");

//20220420 scoreline
//20220421 scoremaxima

makedir(Dircdy,"data");
Dirdata=Dircdy+"data";

xx=-8;
Text34.xy=[xx,4.2];
Text9.xy=[xx,2.8];//Make taskline
Text0.xy=[xx,1.4];//Taskline>>Kettask
Text10.xy=[xx,0];// Make scoreline
Text11.xy=[xx,-1.4];// Scoreline>>Ketscore
Text4.xy=[xx,-2.8];// Maxima
Text3.xy=[xx,-4.2];// Make Card
Text5.xy=[xx,-5.6];// Make combine
Text7.xy=[xx,-7];// Line,Txt to Csv
Text8.xy=[xx,-8.4];// Make combine
Text33.xy=[xx,-9.8];
Text32.xy=[-2,-11.2];// ans check
Text31.xy=[xx,-11.2];// mxans
//Text2.xy=[-4.8,-10];
//Text6.xy=[-2.3,-10];
//Text13.xy=[-6.3,-10];

if(indexof(Toupper(PathM),"MAXIMA")>0,
  maximaok=true;
,
  maximaok=false;
);
quline="";
shline="";
sqline="";
caline="";
mrkline="";
mxscline="";
ansline="";
classflg=0;
class="";
anssheetflg=0;
mxansflg=0;
anscheckflg=0;
makeflg=0;
reset=0;
numL=[];
//dispL=[];
//disp0L=[];
//origin="";
tab=unicode("0009");
dcm="::";
posxy0=[8,-5.5];
posxy=posxy0;
mp=[-10,-10];
size=["Size=1.2"];
clr=size;
clrb=append(size,"Color=blue");
clrr=append(size,"Color=red");

Initscreen():=(
  Px=7.5;Py=6;
  Remark(Px,"0:複数クラスから選択");
  Remark(Px,"1,2:出題用htmlを作成");
  Remark(Px,"  学生の回答を2anssheet.txtに複写");
  Remark(Px,"3,4:採点用htmlを作成");
  Remark(Px,"5:Maximaによる採点(オプション)");
  Remark(Px,"  採点結果を4scoresheetに書込む");
  Remark(Px,"6,7:個人成績票を作成");
  Remark(Px,"8:得点一覧表を作成");
  Remark(Px,"9:成績票を個別フォルダに複写");
  Remark(Px,"a:anssheetを再構成"); 
  Remark(Px,"b:Mxansを作成，c:Mxans,Ans照合"); 
  xL=[73];
  yL=apply(1..15,10);
  Tabledata("1",xL,yL,[],
     [0,"Setwin=n","Move=[-1,-10]","Msg=n"]);
  mv=[-1,-10];
);


Resetsetting():=(
//  quline="";
//  shline="";
//  caline="";
  dispL=[];
  origin="";
);

Resetflg():=Resetflg(["ok","all"]);
Resetflg(noflgorg):=(
  regional(,noflgL,fL,tmp);
  //global mp:mouse().xy
  fL=["mxans","taskline","inserttask","anssheet",
      "scoreline","insertscore","mkcard","class",
      "table","mkfolder","maxima","make",
      "combine","copycombine","anscheck",
      "ok","all"
     ];
  if(islist(noflgorg),
    noflgL=noflgorg;
  ,
    noflgL=[noflgorg,"ok","all"];
  );
  fL=remove(fL,noflgL);
  forall(fL,
    parse(#+"flg=0;");
  );
);

quelineflg=0;
Resetflg([]);

Remark(Px,str):=Remark(Px,str,"black");
Remark(Px,str,clr):=(
  //global Py
  Letter([Px,Py],"e",str,["Size=1.2","Color="+clr]);
  Py=Py-1;
);

month(mon):=(
  regional(out);
  if(mon=="Jan",out="01");
  if(mon=="Feb",out="02");
  if(mon=="Mar",out="03");
  if(mon=="Apr",out="04");
  if(mon=="May",out="05");
  if(mon=="Jun",out="06");
  if(mon=="Jul",out="07");
  if(mon=="Aug",out="08");
  if(mon=="Sep",out="09");
  if(mon=="Oct",out="10");
  if(mon=="Nov",out="11");
  if(mon=="Dec",out="12");
  out;
);

Datetimestr():=(
  regional(out,tmp,tmp1);
  tmp1=apply(date(),text(#));;
  out=tmp1_1;
  tmp=tmp1_2;
  if(length(tmp)==1,tmp="0"+tmp);
  out=out+tmp;
  tmp=tmp1_3;
  if(length(tmp)==1,tmp="0"+tmp);
  out=out+tmp;
  tmp1=apply(time(),text(#));
  tmp=tmp1_1;
  if(length(tmp)==1,tmp="0"+tmp);
  out=out+tmp;
  tmp=tmp1_2;
  if(length(tmp)==1,tmp="0"+tmp);
  out=out+tmp;
  tmp=tmp1_3;
  if(length(tmp)==1,tmp="0"+tmp);
  out=out+tmp;
  substring(out,4,length(out));
);

Getcurtime():=Getcurtime(date(),time());
Getcurtime(ymd,hms):=(
  regional(out,tmp,tmp1);
  out=text(ymd_1*10000+ymd_2*100+ymd_3);
  tmp=text(hms_1*3600+hms_2*60+hms_3);
  tmp1=substring("0000000",0,5-length(tmp));
  out=out+tmp1+tmp;
  out;
);

Returndatetime(ydtorg):=(
  regional(ydt,year,date,time,out,tmp,tmp1);
  ydt=ydtorg;
  if(!isstring(ydt),ydt=text(ydt));
  tmp=indexof(ydt,"202");
  year=substring(ydt,tmp-1,tmp+3);
  date=substring(ydt,tmp+3,tmp+7);
  tmp=substring(ydt,tmp+7,length(ydt));
  if(isstring(tmp),tmp=parse(tmp));
  out=[floor(tmp/3600)];
  tmp=mod(tmp,3600);
  out=append(out,floor(tmp/60));
  tmp=mod(tmp,60);
  tmp1=append(out,tmp);
  out="";
  forall(tmp1,
    out=out+text(#)+":";
  );
  out=substring(out,0,length(out)-1);
  [year,date,out];
);

List2line(stLL):=(
  regional(tab,nn,nc,tmp,str,st,out);
  tab=unicode("0009");
  out="";
  forall(1..(length(stLL)),nn,
    st=stLL_nn;
    str="";
    forall(1..(length(st)),
      tmp=st_#;
      if(!isstring(tmp),tmp=format(tmp,10));
      str=str+tmp+tab;
    );
    str=substring(str,0,length(str)-1);
    out=out+str;
    if(nn<length(stLL),out=out+"CR");
  );
  out;  
);

Line2list(strorg):=(
  regional(tab,stL,st,nn,tL,mx,flg,tmp);
  tab=unicode("0009");
  str=replace(strorg,";;",tab);
  str=replace(str," ","%");
  flg=indexof(str,"::");
  tL=[];
  if(indexof(str,"CR")>0,
    stL=Strsplit(str,"CR");
    stL=apply(stL,replace(#,"%"," "));
    forall(1..(length(stL)),nn,
      st=stL_nn;
      if(!isstring(st),st=format(st,10));
      stL_nn=tokenize(st,tab);
    );
  ,
    stL=tokenize(str,tab);
    stL=apply(stL,replace(#,"%"," "));
    forall(1..(length(stL)),
      tmp=stL_#;
      if(indexof(tmp,"::")>0,
        tmp=tokenize(tmp,"::")
      );
      stL_#=tmp;
    );
  );
  stL;
);

Tablepos(tb,mv):=(
  regional(tmp,tmp1,tmp2,cl,rw);
  //global mp:mouse().xy, Dirdata
  tmp1=apply(tb_1,#_1+mv_1);
  tmp2=apply(tb_2,#_2+mv_2);
  cl=0;
  if((mp_1>tmp1_1)&(mp_1<tmp1_(-1)),
    tmp=select(1..(length(tmp1)),tmp1_#>mp_1);
    cl=tmp_1-1;
  );
  rw=0;
  if((mp_2<tmp2_1)&(mp_2>tmp2_(-1)),
    tmp=select(1..(length(tmp2)),tmp2_#<mp_2);
    rw=tmp_(1)-1;
  );
  [cl,rw];
);

MakefileL(dfL):=(
  regional(out,tmp,dir,selectL,fL,se,s);
  out=[];
  forall(1..(length(dfL)/2),nn,
    dir=dfL_(2*nn-1);
    tmp=dfL_(2*nn);
    if(!islist(tmp),tmp=[tmp]);
    selectL=apply(tmp,Strsplit(#,"*"));
    fL=Filelist(dir);
    forall(selectL,se,
      tmp=fL;
      forall(se,s,
        tmp=select(tmp,indexof(#,s)>0);
      );
      out=concat(out,tmp);
    );
  );
  out;
);

Selectfile(out):=Selectfile(out,100);
Selectfile(out,num):=(
  regional(nblu,nblk,cl,rw,outd,tmp,tmp1,res);
  //global mp:mouse().xy
  outd=[];
  if(!islist(num),
    tmp=max([1,num]);
    nblu=tmp..(length(out));
  ,
    nblu=num;
  );
  nblk=remove(1..(length(out)),nblu);
  if(islist(num),numL=num,numL=1..num);
  if(numL==[0],numL=[]);
  forall(out,
    tmp=Indexall(#,".");
    if(length(tmp)>0,
      tmp=tmp_(-1);
      tmp1=substring(#,0,tmp-1);
    ,
      tmp1=#;
    );
    outd=append(outd,tmp1);
  );
  tmp=min([length(out),15]);
  tmp1=1..tmp;
  if(length(tmp1)>0,
    forall(nblu,
      Putcell(1,#,"l2",outd_#,["Color=blue"]);
    );
    forall(nblk,
      Putcell(1,#,"l2",outd_#);
    );
    tmp=Tablepos(tb1,mv);
    cl=tmp_1; rw=tmp_2;
    if((cl*rw>0)&(rw<=length(out)),
      res=[rw,out_rw];
    ,
      res=[0,""];
    );
  ,
    res=[0,""];
  );
  res;
);

Mkquline(fname):=(
  regional(dt0,dt,nn,qsL,nq,hdL,nn,qs,ge,cs,ce,ss,se,
       date,qctr,quL,shL,caL,head,tmp,tmp1,tmp2,tmp3);
  //global quline,shline,caline,
  //   mrkline,maxline,dcm
  quline="";shline="";caline="";
  mrkline=""; mxscline="";
    tmp1=apply(0..9,text(#)); //220705from
  tmp=1;
  while(!contains(tmp1,substring(fname,tmp-1,tmp)),
    if(tmp>length(fname),tmp="0");
    tmp=tmp+1;
  );
  tmp1=substring(fname,tmp-1,length(fname));
  tmp=indexof(tmp1,"-");
  date=substring(tmp1,0,tmp-1);
  tmp1=substring(tmp1,tmp,length(tmp1));
  tmp=indexof(tmp1,".");
  if(tmp>0,tmp1=substring(tmp1,0,tmp-1));
  qctr=parse(tmp1); //220705to
  quL=[]; shL=[]; caL=[];
  dt0=Readlines(Dirdata,fname);
  dt0=append(dt0,"");
  dt=[];
  nn=1;
  tmp1="";
  while(nn<=length(dt0),
    tmp2=dt0_nn;
    tmp1=tmp1+tmp2;
    tmp=length(tmp2);
    tmp=substring(tmp2,tmp-2,tmp);
    if((tmp!="//")&(tmp!="-<"),
      dt=append(dt,tmp1);
      tmp1="";
    ,
      if(tmp=="-<",
        tmp=length(tmp1);
        tmp1=substring(tmp1,0,tmp-2);
      );
    );
    nn=nn+1;
  );
  tmp=select(1..(length(dt)),
        substring(dt_#,0,1)=="Q");
  qsL=apply(tmp,#+1);
  hdL=apply(tmp,dt_#);
  forall(1..(length(hdL)),
    tmp=replace(Sprintf(qctr/100,2),"0.","");
    hdL_#="Q"+tmp;
    qctr=qctr+1;
  );
  forall(1..(length(qsL)),nq,
    head=hdL_nq;
    qs=qsL_nq;
    tmp=select(qs..(length(dt)),dt_#=="");
    if(length(tmp)>0, //221024from
      ce=tmp_1-1;
    ,
      ce=length(dt);
    );
    tmp=select(qs..ce,dt_#=="Mxcalc");
    if(length(tmp)>0,
      ce=tmp_1-1;
    ); //221024to
    tmp1=select(qs..ce,dt_#=="Sheet");
    tmp2=select(qs..ce,dt_#=="Ans");
    cs=tmp2_1+1;
    if(length(tmp1)>0,
      qe=tmp1_1-1;
      ss=tmp1_1+1;
      se=tmp2_1-1;
      tmp1=dt_(qs..qe);
    ,
      qe=tmp2_1-1;
      tmp1=dt_(qs..qe);
    );
    if(length(tmp1)==1,
      tmp1=prepend("問いに答えよ",tmp1);
      tmp1_2=tmp1_2;
    );
    tmp1_1=head+dcm+tmp1_1;
    forall(2..(length(tmp1)),nn,
      tmp=tmp1_nn;
      if(substring(tmp,0,1)=="[",
        qs=indexof(tmp,"]");
        tmp1_nn=substring(tmp,0,qs)+dcm
              +substring(tmp,qs,length(tmp));
      ,
        tmp1_nn=dcm+tmp;
      );
    );
    forall(tmp1,quline=quline+#+tab);
    tmp1=dt_(ss..se);
    tmp1=prepend(head+"---",tmp1);
    forall(1..(length(tmp1)),
      tmp2=Strsplit(tmp1_#,dcm);
      shline=shline+tmp2_1+tab;
      if(length(tmp2)>1,tmp=tmp2_2,tmp="0");
      mrkline=mrkline+tmp+tab;
      if(length(tmp2)>2,tmp=tmp2_3,tmp="1");
      mxscline=mxscline+tmp+tab;
    );
    tmp3=dt_(cs..ce);
    caline=caline+head+dcm+tab;
    forall(1..(length(tmp3)),
      tmp1=tmp3_#;
      if(substring(tmp1,0,1)=="[",
        tmp=indexof(tmp1,"]");
        tmp2=substring(tmp1,0,tmp)+dcm;
        tmp=substring(tmp1,tmp,length(tmp1));
        tmp2=tmp2+Removespace(tmp)
      ,
        tmp2=dcm+tmp1;
      );
      caline=caline+Removespace(tmp2)+tab;
    );
  );
  quline=substring(quline,0,length(quline)-1);
  shline=substring(shline,0,length(shline)-1);
  caline=substring(caline,0,length(caline)-1);
  mrkline
    =substring(mrkline,0,length(mrkline)-1);
  mxscline
    =substring(mxscline,0,length(mxscline)-1);
  tmp1=tokenize(mrkline,tab);
  tmp2=tokenize(mxscline,tab);
  tmp3="";
  forall(1..(length(tmp2)),
    if(tmp1_#==0,tmp=0,tmp=tmp2_#);
    tmp3=tmp3+tmp+tab;
  );
  mxscline=substring(tmp3,0,length(tmp3)-1);
);

Que2line(ch):=(
  regional(fname,fL,dt,qsL,nq,gs,qe,ss,se,cs,ce,
    flg,nn,tmp,tmp1,tmp2,tmp3);
  //global Dirdata,posxy0,posxy,reset,mp
  //      quline,shline,caline
  dt=[];
  fname=""; quline=""; shline=""; caline="";
  posxy=posxy0;
  if(ch==1,
    tmp=MakefileL([Dirdata,"question"]);
    tmp=sort(tmp);
    tmp=apply(tmp,replace(#,".txt",""));
    tmp=Selectfile(tmp);
  ,
    dir=Dirdata;
    fL=Filelist(Dirdata);
    tmp=select(fL,indexof(#,"question")>0);
    tmp=[1,tmp_1];
  );
  if(tmp_1>0,
    fname=tmp_2;
    Mkquline(fname);
    flg=1;
  );
  [flg,fname];
);

Tasklineall(fstu,fname):=(
  regional(class,fout,fout2,fout3,
      fid,dt,stL,ns,quL,nn,nc,col,
     tmp,tmp1,tmp2,tmp3);
  //global Dirdata,posxy0,posxy,reset,mp
  //    size,sqline
  tmp1=length("student")+4;
  tmp2=indexof(fstu,".");
  class=substring(fstu,tmp1,tmp2-1);
  Mkquline(fname);
  quL=Line2list(quline);
  dt=Readlines(Dirdata,fstu);
  if(indexof(dt_1,tab)>0,tmp=tab,tmp=",");
  dt=apply(dt,Strsplit(#,tmp));
  tmp=dt_1;
  tmp=select(1..(length(tmp)),
            indexof(tmp_#,"名前")>0);
  if(length(tmp)>0,
    col=tmp_1;tmp=2;
  ,
    tmp=1;
    if(length(dt_1)==1,
      col=1;tmp=1;
    ,
      col=3;tmp=2;
    );
  );
  dt=dt_(tmp..length(dt));
  stL=apply(dt,#_[col]);
  forall(1..(length(stL)),ns,
    tmp1=stL_ns;
    forall(1..(length(quL)),nq,
      tmp2=quL_nq;
      nn=length(tmp2);
      if(nn>1,
        tmp=random(nn-1);
        nc=min([floor(tmp)+1,nn-1]);
        nc=nc+1;
        tmp1=append(tmp1,text(nc));
      ,
        tmp1=append(tmp1,"1");
      );
    );
    stL_ns=tmp1;
  );
  sqline=List2line(stL);
  setdirectory(Dirdata);
  fout=replace(fname,
           "question",class+"1taskline");
  fid=openfile(fout);
  tmp="quline="+Dqq(quline)+";";
  println(fid,tmp);
  tmp="shline="+Dqq(shline)+";";
  println(fid,tmp);
  tmp="sqline="+Dqq(sqline)+";";
  println(fid,tmp);
  closefile(fid);
  tmp1="1taskline";
  tmp2="2anssheet";
  fout2=replace(fout,tmp1,tmp2);
  if(!isexists(Dirdata,fout2),
    fid=openfile(fout2);
    closefile(fid);
  );
);

Inserttasklineall(forg,out1,out2,fname):=(
  regional(out,fL,ldata,link,fid,fout,LinkL,
    class,tmp,tmp1,tmp2,tmp3);
  //global mp,tb1,mv,reset,numL
  ldata=Readlines(Dirdata,fname);
  out=concat(out1,ldata);
  tmp1=replace(fname,"1taskline","scriptadd");
  if(isexists(Dirdata,tmp1),
    tmp3=Readlines(Dirdata,tmp1);
    tmp=select(1..(length(out2)),
          indexof(out2_#,"Windispg();")>0);
    tmp=tmp_1;
    tmp1=out2_(1..(tmp-1));
    tmp2=out2_(tmp..(length(out2)));
    out=concat(out,tmp1);
    out=concat(out,tmp3);
    out=concat(out,tmp2);
  ,
    out=concat(out,out2);
  );
  tmp1=replace(fname,"1taskline","scriptlink");
  if(isexists(Dirdata,tmp1),
    setdirectory(Dirdata);
    import(tmp1);
    tmp=select(1..(length(out)),
           indexof(out_#,
              "id="+Dqq("CSCanvas"))>0);
    if(length(tmp)>0,
      tmp=tmp_1;
      tmp1=out_(1..(tmp));
      tmp2=out_((tmp+1)..(length(out)));
      forall(LinkL,link,
        if(length(link)==1,
           tmp=Readlines(Dircdy,link_1);
           tmp1=concat(tmp1,tmp);
        ,
          tmp="<font size="+Dqq("5");
          tmp=tmp+"><p style=";
          tmp=tmp+Dqq("text-align: center");
          if(length(link)==2,
            tmp=tmp+"><a href="+Dqq(link_1)+">";
            tmp=tmp+link_2+"</a></p></font>";
          ,
            tmp3=indexof(link_1," ");
            tmp3=substring(link_1,0,tmp3-1);
            tmp=tmp+"><"+link_1+"=";
            tmp=tmp+Dqq(link_2)+">"+link_3;
            tmp=tmp+"</"+tmp3+"></p></font>";
          );
          tmp1=append(tmp1,tmp);
        );
      );
      out=concat(tmp1,tmp2);
    );
  );
  tmp=indexof(fname,"1taskline");
  class=substring(fname,0,tmp-1);
  tmp1=indexof(fname,".");
  tmp2=substring(fname,tmp-1,tmp1-1);
  tmp=replace(tmp2,"1taskline","");
  fout=class+replace(forg,"orgv","v"+tmp);
  setdirectory(Dircdy);
  fid=openfile(fout);
  forall(out,println(fid,#));
  closefile(fid);
);

Scorelineall(fstu,fname):=(
  regional(class,fname1,fname2,fname3,fid,dt,
     nn,shL,sqL,ansL,out,flg,tmp,tmp1,tmp2);
  //global Dirdata,posxy0,posxy,reset,mp,dispL
  tmp1=length("student")+4;
  tmp2=indexof(fstu,".");
  class=substring(fstu,tmp1,tmp2-1);
  fname1=replace(fname,"question","1taskline");
  fname1=class+fname1;
  fname2=replace(fname1,"1taskline","2anssheet");
  fname3=replace(fname1,"1taskline","3scoreline");
  setdirectory(Dirdata);
  Mkquline(fname);
  out=["quline="+Dqq(quline)+";",
          "shline="+Dqq(shline)+";",
          "caline="+Dqq(caline)+";",
          "mrkline="+Dqq(mrkline)+";",
          "mxscline="+Dqq(mxscline)+";"
         ];
  import(fname1);
  out=append(out,"sqline="+Dqq(sqline)+";");
  sqL=Line2list(sqline);
  tmp=Readlines(Dirdata,fname2);
  tmp=select(tmp,indexof(#,";;")>0);
  dt=apply(tmp,Strsplit(#,";;"));
  dt=sort(dt,[parse(#_1)]);
  forall(1..(length(dt)),
    tmp=parse(dt_#_1);
    dt_#_1=text(tmp);
  );
  ansL=[];
  forall(1..(length(sqL)),nn,
    tmp1=[text(nn),sqL_nn_1];
    tmp=select(dt,#_1==text(nn));
    if(length(tmp)>0,
      tmp=tmp_1;
      tmp2=tmp_(3..(length(tmp)));
      tmp=Returndatetime(tmp_2);
      tmp=tmp_2+tmp_3;
      tmp2=prepend(tmp,tmp2);
      tmp1=concat(tmp1,tmp2);
    ,
      tmp1=append(tmp1,"未提出");
      tmp1=concat(tmp1,shL);
    );
    ansL=append(ansL,tmp1);
  );
  ansline=List2line(ansL);
  tmp="ansline="+Dqq(ansline)+";";
  out=append(out,tmp);
  fid=openfile(fname3);
  forall(out,
    println(fid,#);
  );
  closefile(fid);
  out;
);

Insertscorelineall(forg,out1,out2,fname):=(
  regional(class,fout,out,fid,ldata,
           tmp,tmp1,tmp2);
  //global mp,tb1,mv,reset,dispL
  tmp=indexof(fname,"3scoreline");
  class=substring(fname,0,tmp-1);
  ldata=Readlines(Dirdata,fname);
  out=concat(out1,ldata);
  out=concat(out,out2);
  tmp1=replace(fname,class+"3scoreline","scriptlink");
  if(isexists(Dirdata,tmp1),
    setdirectory(Dirdata);
    import(tmp1);
    tmp=select(1..(length(out)),
           indexof(out_#,
              "id="+Dqq("CSCanvas"))>0);
    if(length(tmp)>0,
      tmp=tmp_1;
      tmp1=out_(1..(tmp));
      tmp2=out_((tmp+1)..(length(out)));
      forall(LinkL,link,
        if(length(link)==1,
           tmp=Readlines(Dircdy,link_1);
           tmp1=concat(tmp1,tmp);
        ,
          tmp="<font size="+Dqq("5");
          tmp=tmp+"><p style=";
          tmp=tmp+Dqq("text-align: center");
          if(length(link)==2,
            tmp=tmp+"><a href="+Dqq(link_1)+">";
            tmp=tmp+link_2+"</a></p></font>";
          ,
            tmp3=indexof(link_1," ");
            tmp3=substring(link_1,0,tmp3-1);
            tmp=tmp+"><"+link_1+"="+Dqq(link_2)+">";
            tmp=tmp+link_3+"</"+tmp3+"></p></font>";
          );
          tmp1=append(tmp1,tmp);
        );
      );
      out=concat(tmp1,tmp2);
    );
  );
  tmp1=indexof(fname,".");
  tmp2=substring(fname,length(class),tmp1-1);
  tmp=replace(tmp2,"3scoreline","");
  fout=class+replace(forg,"orgv","v"+tmp);
  setdirectory(Dircdy);
  fid=openfile(fout);
  forall(out,
    println(fid,#);
  );
  closefile(fid);
  tmp=replace(fname,"3scoreline","4scoresheet");
  setdirectory(Dirdata);
  if(!isexists(Dirdata,tmp),
    fid=openfile(tmp);
    closefile(fid);
  );
);

Errorcheck(str):=(
  regional(flg,tmp,tmp1,tmp2);
  flg=0;
  tmp1=Indexall(str,"^");
  forall(tmp1,
    if(#==1,
      flg=1;
    ,
      if(str_(#-1)=="(",
        flg=1;
      ,
        if(#>=4,
          tmp=substring(str,#-4,#-1);
          if(contains(["sin","cos","tan"],tmp),
            flg=1; // hat error
          );
        );
      );  
    );
  );
  if(flg==0,
    tmp=Bracket(str);
    if(length(tmp)>0,
      tmp=tmp_(-1)_2;
      if(tmp!=-1,flg=2); // par error
    );
  );
  if(flg==0,
    tmp=Tomaxform(str);
    if(indexof(tmp,"___")>0,
      flg=3; // tomax error
    );
  );
  flg;
);

Dataformaxima(fname):=(
  regional(scLL,fout,out,maxLL,
      shL,caL,mxscL,sqLL,ansLL,stL,
      dataL,maxL,sqL,ansL,
      stdatall,sall,qall,ns,
       tmp,tmp1,tmp2,tmp3);
  //global tab;
  setdirectory(Dirdata);
  import(fname);
  shL=line2list(shline);
  caL=Line2list(caline);
  mxscL=Strsplit(mxscline,tab);
  sqLL=Line2list(sqline);
  sqLL=apply(sqLL,#_(2..(length(#))));
  ansLL=Line2list(ansline);
  StdataLL=apply(ansLL,#_(1..3));
  sall=length(StdataLL);
  ansLL=apply(ansLL,#_(4..(length(#))));
  maxLL=[];
  qall=length(shL);
  forall(1..sall,ns,
    stL=StdataLL_ns;
    if(stL_3!="未提出",
      dataL=[];
      maxL=[];
      sqL=sqLL_ns;
      ansL=ansLL_ns;
      if(length(ansL)!=qall,
        tmp3=[];
        forall(1..qall,tmp,
          tmp1=shL_tmp;
          tmp2=select(ansL,indexof(#,tmp1)>0);
          if(length(tmp2)>0,
            tmp3=append(tmp3,tmp2_1);
          ,
            tmp3=append(tmp3,tmp1);
          );
        );
        ansL=tmp3;
      );
      forall(1..qall,kq,
        ans=ansL_kq;
        if(indexof(ans,"---")==0,
          if(indexof(ans,"=")>0,
            tmp=Strsplit(ans,"=");
            ans=tmp_(-1);
          );
          tmp=Getlevel(ans,",");//1112from
          tmp=select(tmp,#_2==0);
          if(length(tmp)>0,ans="["+ans+"]");//1112to
          ca=caL_kq;
          tmp=Getlevel(ca,",");//1112from
          tmp=select(tmp,#_2==0);
          if(length(tmp)>0,ca="["+ca+"]");//1112to
          tmp=sqL_kq;
          ca=ca_tmp;
          if(isreal(ca),ca=text(ca));
          if(indexof(ca,"=")>0,
            tmp=Strsplit(ca,"=");
            ca=tmp_(-1);
          );
          tmp1=replace(ans,"tfr","fr");
          tmp=Errorcheck(tmp1);
          if(tmp==0,
            ansm=Tomaxform(tmp1);
          ,
            ansm="inf";
          );
          cam=Tomaxform(replace(ca,"tfr","fr"));
          if(ansm=="",
            tmp="na";
          ,
            if(cam=="0",
              tmp=ansm+"+1";
            ,
              tmp="("+ansm+")-("+cam+")";
            );
          );
          if(mxscL_kq=="-1",tmp="ns");
          maxL=append(maxL,tmp);
        ,
          maxL=append(maxL,"no");
        );
      );
      maxL=prepend(ns,maxL);
      maxLL=append(maxLL,maxL);
    ,
      maxLL=append(maxLL,[ns]);    
    );
  );
  maxLL;
);

Scoringmaxima(nn,fname,maxLLorg,oporg):=(
  regional(maxLL,op,flg,cmdL,ns,qn,ansLL,ansL,
     headL,scLL,scL,mrkL,tmp,tmp1,tmp2,tmp3,
     str,estr,pe);
  //global ansline
  setdirectory(Dirdata);
  import(fname);
  ansLL=Line2list(ansline);
  headL=apply(1..(length(ansLL)),ansLL_#_(1..3));
  ansLL=apply(1..(length(ansLL)),
                  remove(ansLL_#,headL_#));
  mrkL=Strsplit(mrkline,tab);
  op=oporg;
  maxLL=maxLLorg;
  tmp=select(op,!isstring(#));
  if(length(tmp)>0,
    op=remove(op,tmp);
    tmp=tmp_1;
    maxLL=maxLL_tmp;
  );
  setdirectory(Dirwork);
  cmdL=[
    "dtLL:"+text(maxLL),[],
    "res:ratsimp(dtLL)",[],
    "res",[]
  ];
  flg=CalcbyM("res",cmdL,op);
  if(flg==2,
    tmp=Fhead+"res";
    tmp1=Readlines(Dircdy+"/fig",tmp+".txt");
    tmp2=select(1..(length(tmp1)),
           indexof(tmp1_#,tmp)>0);
    if(length(tmp2)>0,
      tmp2=tmp2_1+1;
      estr=tmp1_tmp2;
      tmp=Readlines(Dircdy+"/fig",tmp+".max");
      str=select(tmp,indexof(#,"dtLL:")>0);
      str=str_1;
      pe=indexof(str,estr);
      if(pe>0,
        tmp=Bracket(str,"[]");
        tmp=select(tmp,(#_1<=pe)&(#_2==2));
        tmp=apply(tmp,#_1);
        ns=length(tmp);
        tmp1=tmp_(-1);
        tmp=Getlevel(str);
        tmp=select(tmp,#_2==0);
        tmp=select(tmp,(#_1<=pe)&(#_1>=tmp1));
        nq=length(tmp)+2;
        dispL=append(dispL,
          ["Error",clrr]);
        dispL=append(dispL,
          ["nn="+nn+" st="+ns+" qu="+nq,clrb]);
      );
    );
  ,
    res=substring(res,1,length(res)-1);
    tmp=Getlevel(res,",","[]");
    tmp=select(tmp,#_2==0);
    if(length(tmp)>0,
      tmp=apply(tmp,#_1);
      forall(tmp,res_#=tab);
      res=Strsplit(res,tab);
    ,
      res=[res];
    );
    scLL=[];
    forall(res,tmp1,
      tmp2=substring(tmp1,1,length(tmp1)-1);
      tmp=Getlevel(tmp2,",");
      tmp=select(tmp,#_2==0);
      if(length(tmp)>0,
        tmp=apply(tmp,#_1);
        forall(tmp,tmp2_#=tab);
        tmp=Strsplit(tmp2,tab);
      ,
        tmp=[tmp2];
      );
      tmp=tmp_(2..(length(tmp)));
      scLL=append(scLL,tmp);
    );
    forall(1..(length(ansLL)),nn,
      ansL=ansLL_nn;
      scL=scLL_nn;
      forall(1..(length(ansL)),qn,
        tmp1=ansL_qn;
        tmp2=scL_qn;
        if(contains(["no","na","ns"],tmp2),
          if(tmp2=="na",ansL_qn=tmp1+"::0");
          if(tmp2=="ns",ansL_qn=tmp1+"::");
        ,
          if(tmp2=="0", //1112
            ansL_qn=tmp1+"::"+mrkL_qn;
          ,
            ansL_qn=tmp1+"::0";
          );
        );
      );
      ansLL_nn=concat(headL_nn,ansL);
    );
    tmp1=List2line(ansLL);
  );
  tmp1;
);

Makecards(fname):=(
  regional(dtLL,dtL,fname1,fname2,qu,ca,all,fout,
     quL,caL,shL,sqLL,sqL,scLL,scL,nn,nq,date,
     out,fid,nd,tmp,tmp1,tmp2);
  //global Dirdata,posxy0,posxy,reset,mp,dispL
  dtLL=[];
  fname1=replace(fname,
      "3scoreline","4scoresheet");
  if(isexists(Dirdata,fname1),
    setdirectory(Dirdata);
    import(fname);
    quL=Line2list(quline);
    caL=Line2list(caline);
    shL=Line2list(shline);
    shL=apply(shL,replace(#,"---",""));
    sqLL=Line2list(sqline);
    tmp=Readlines(Dirdata,fname1);
    scLL=Line2list(tmp_1);
    forall(1..(length(sqLL)),nn,
      scL=scLL_nn;
      if(scL_3=="未提出",
        dtL=[scL_1+" "+scL_2+" "+scL_3];
      ,
        dtL=[scL_1+" "+scL_2+" "
              +substring(scL_3,0,4)+" "
              +substring(scL_3,4,length(scL_3))];
        scL=scL_(4..(length(scL)));
        sqL=sqLL_nn;
        sqL=sqL_(2..(length(sqL)));
        all=length(sqL);
        qu=apply(1..all,
            quL_#_1+" "+quL_#_(sqL_#));
        ca=apply(1..all,caL_#_(sqL_#));
        forall(1..all,nq,
          tmp1=qu_nq;
          tmp2=replace(scL_nq,shL_nq,"");
          if(substring(tmp1,0,1)=="Q",
            if(length(ca_nq)>0,
              tmp1=[tmp1,"　正解 "+ca_nq];
            ,
              tmp1=[tmp1];
              tmp2="";
            );
          ,
            tmp1=[tmp1,"　正解 "+ca_nq];
          );
          dtL=concat(dtL,tmp1);
          if(length(tmp2)>0,
            tmp3=Strsplit(tmp2,"::");
            if(length(tmp3)==1,
              tmp3=append(tmp3,"0");
            ,
              if(length(tmp3_2)==0,
                tmp3_2="0";
              );
            );
            tmp2=["　答え "+tmp3_1,
                  "　得点 "+tmp3_2];
            dtL=concat(dtL,tmp2);
          );
        );
      );
      dtLL=append(dtLL,dtL);
    );
    tmp=replace(fname,"3scoreline","");
    date=replace(tmp,".txt","");
    fout="6recordlist"+date+".txt";
    forall(1..(length(dtLL)),nd,
      tmp="000"+text(nd);
      tmp=substring(tmp,
            length(tmp)-2,length(tmp));
      fname2=class+"st"+tmp+"task"+date+".txt";
      setdirectory(Dirdata+"/card"+class);
      fid=openfile(fname2);
      println(fid,date+"の結果");
      forall(dtLL_nd,
        println(fid,#);
      );
      closefile(fid);
    );
    flg=0;
  ,
    dispL=append(dispL,
             [fname1+" not found",clrr]);
    flg=1;
  );
);

Combinedata():=Combinedata(1);
Combinedata(ow):=(
  regional(dir,fL,fr,to,stno,stL,dtL,
       st,fid,tmp,tmp1,tmp2,nn);
  //global Dirdata,dispLdate
  // ow: overwriteflg
  dir=Dirdata+"/card"+class;
  fL=Filelist(dir);
  fL=select(fL,indexof(#,"st")*indexof(#,"task")>0);
  fL=select(fL,substring(#,0,length(class))==class);
  fL=sort(fL);
  stL=[];
  forall(fL,
    tmp=indexof(#,"task");
    if(tmp>0,
      tmp=substring(#,0,tmp-1);
      stL=append(stL,tmp);
    );
  );
  stL=set(stL);
  stL=apply(stL,replace(#,class+"st",""));
  stL=sort(stL);
  dtL=[];
  forall(stL,st,
    tmp1=select(fL,
      indexof(#,"st"+st)==length(class)+1);
    tmp1=sort(tmp1);
    tmp2=[];
    forall(1..(length(tmp1)),nn,
      tmp=select(tmp1,indexof(#,"-"+nn+".")>0);
      if(length(tmp)>0,
        tmp=tmp_1;
      ,
        tmp=tmp1_nn;
      );
      tmp=Readlines(dir,tmp);
      tmp=append(tmp,"");
      tmp2=concat(tmp2,tmp);
    );
    dtL=append(dtL,tmp2);
  );
  setdirectory(dir);
  forall(1..(length(stL)),tmp1,
    tmp=class+date+"total"+"st"+stL_tmp1+".txt";
    if((!isexists(dir,tmp))%(ow==1),
      fid=openfile(tmp);
      tmp=dtL_tmp1;
      forall(tmp,
        println(fid,#);
      );
      closefile(fid);
    );
  );
//  setdirectory(Dirwork);
  dispL=append(dispL,["combinedata finished",clrb]);
  [date,stL,dtL,dispL];
);

Resulttable(head,shL,fnameL):=(
  regional(dir,nn,dtL,dt,kL,kk,sh,out,sm,
       tmp,tmp1,tmp2);
  //global tab;
  fout=head+"table.csv";
  dir=Dirdata+"/card"+class;
  out=[];
  forall(1..(length(fnameL)),nn,
    sm=0;
    sh=shL;
    dtL=Readlines(dir,fnameL_nn);
    kL=select(1..(length(dtL)),
          indexof(dtL_#,"結果")>0);
    kL=append(kL,(length(dtL)));
    forall(1..(length(kL)-1),kk,
      dt=dtL_((kL_kk)..(kL_(kk+1)));
      if(kk==1,
        tmp1=replace(dt_2," ",",");
      );
      if(indexof(tmp1,"未提出")==0,
        tmp2=select(dt,indexof(#,"　得点 ")>0);
        tmp2=apply(tmp2,replace(#,"　得点 ",""));
        forall(1..(length(tmp2)),
          sm=sm+parse(tmp2_#);
          sh_kk_#=tmp2_#;
        );
      ,
        tmp1=replace(tmp1,"未提出","NotYet");
      );
    );
    sh=flatten(sh);
    forall(sh,tmp1=tmp1+","+#);
    tmp1=tmp1+","+sm;
    out=append(out,tmp1); 
  );
  out;
);

Copycombine(parent,child):=(
  regional(fid,dir,fL,stL,date,ns,ne,
        tmp,tmp1,tmp2);
  if(length(Dirdist)>0,
    dir=Dirdata+"/card"+class;
    fL=Filelist(dir);
    fL=select(fL,indexof(#,"total")>0);
    if(length(fL)==0,
      dispL=[["No total files",clrr]];
    ,
      fL=sort(fL);
      forall(fL,tmp1,
        ns=indexof(tmp1,"st")+1;
        ne=indexof(tmp1,".txt")-1;
        tmp=substring(tmp1,ns,ne);
        tmp2=parent+"/"+child;
        if(length(class)>0,
          tmp2=tmp2+"/"+class;
        );
        Makedir(tmp2,tmp);
        setdirectory(tmp2+"/"+tmp);
        tmp=Readlines(dir,tmp1);
        fid=openfile(tmp1);
        forall(tmp,
          println(fid,#);
        );
        closefile(fid);
      );
      dispL=append(dispL,
          ["CombineData copied",clrb]);
    );
  );
);

Addmxcalc(nf,fnameL):=(
  regional(fname,lineL,nall,mQL,nm,mQ,mc,me,str,
           vL,cL,mcL,cmdL,flg,ret,opc,ansnm,
           tmp,tmp1,tmp2,tmp3,ans,fid);
  fname=fnameL_nf;
  cL=[]; errL=[]; ret="";
  lineL=Readlines(Dirdata,fname);
  nall=length(lineL);
  mQL=select(1..nall,
          substring(lineL_#,0,1)=="Q");
  forall(1..(length(mQL)),nm,
    vL=[];
    mcL=[];
    mQ=mQL_nm;
    tmp=select((mQ+1)..nall,
          indexof(lineL_#,"Mxcalc")>0);
    mc=tmp_1+1;
    tmp=select(mc..nall,
          indexof(lineL_#,"return")>0);
    me=tmp_1;
    while(mc<=me,
      str=lineL_mc;
      if(substring(str,0,1)=="[",
        tmp=indexof(str,"]");
        tmp1="i"+substring(str,1,tmp-1);
        vL=append(vL,tmp1);
        tmp2=substring(str,tmp,length(str));
        tmp2=Removespace(tmp2);
        tmp=Getlevel(tmp2);
        if(length(tmp)==0,
          if(indexof(tmp2,Dq)==0,
            flg=Errorcheck(tmp2);
            if(flg==0,
              tmp2=Tomaxform(tmp2);
            ,
              tmp2="err="+flg+" "+tmp2;
            );
          );
        ,
          tmp=select(tmp,#_2==0);
          if(length(tmp)>0,
            tmp=apply(tmp,#_1);
            forall(tmp,tmp2_#="#");
            tmp2=Strsplit(tmp2,"#");
          ,
            tmp2=[tmp2];
          );
          forall(1..(length(tmp2)),
            tmp=tmp2_#;
            flg=Errorcheck(tmp);
            if(flg==0,
              tmp2_#=Tomaxform(tmp);
            ,
              tmp2_#="err="+flg+" "+tmp;
            );
          );
          if(length(tmp2)==1,tmp2=tmp2_1);
        );
        mcL=append(mcL,tmp1+":"+tmp2);
      ,
        if(indexof(str,"return")==0,
          tmp=Strsplit(str,":");
          tmp1=tmp_1;
          if(length(tmp)>=2,
            tmp1=tmp_1;
            vL=append(vL,tmp1);
          );
          if(length(tmp)==3,
            tmp2=tmp_2;
            if(tmp2=="Tomax",tmp2="Tomaxform");
            str=tmp2+"("+Dqq(tmp_3)+")";
            tmp2=parse(str);
            str=tmp1+":"+tmp2;
          );
          mcL=append(mcL,str);
        ,
          tmp1=replace(str,"return","");
          if(length(tmp1)>0,
            tmp1=Removespace(tmp1);
            if(indexof(tmp1,"::")==0,
              tmp=indexof(tmp1,"[");
              if(tmp==0,
                vL=append(vL,
                    substring(tmp1,0,tmp-1));
              ,
                tmp2=substring(tmp1,
                        tmp,length(tmp1)-1);
                tmp1=substring(tmp1,0,tmp-1);
                vL=append(vL,tmp1);
                tmp2=Strsplit(tmp2,",");
//                tmp2=apply(tmp2,tmp1+"["+#+"]");
                tmp2=apply(tmp2,tmp1+#);
                tmp="";
                forall(tmp2,tmp=tmp+"q"+nm+#+"::");
                tmp=substring(tmp,0,length(tmp)-2);
                ret=ret+tmp+"::"
              );
            ,
              tmp="q"+nm+replace(tmp1,"::","::q"+nm);
              ret=ret+tmp+"::";
            );
          );
          mc=1000;
        );
      );
      mc=mc+1;
    );
    vL=set(vL);
    tmp1=apply(vL,"q"+nm+#);
    forall(1..(length(tmp1)),nn,
      forall(1..(length(mcL)),
        mcL_#=replace(mcL_#,vL_nn,tmp1_nn);
      );
    );
    cL=concat(cL,mcL);
  );
println(1307);
println(cL);
  forall(1..length(cL), //1102from
    tmp3=cL_#;
    tmp=indexof(tmp3,":");
    tmp1=substring(tmp3,0,tmp);
    tmp2=substring(tmp3,tmp,length(tmp3));
    tmp=Getlevel(tmp2,",");
    tmp=select(tmp,#_2==0);
//    if(length(tmp)>0,
//      cL_#=tmp1+"["+tmp2+"]";
//    );
  );  //1102to
 println(1318);
  println(cL);
   ret=substring(ret,0,length(ret)-2);
  cL=append(cL,ret);
  cL=select(cL,length(#)>0);
  cmdL=[];
  forall(cL,
    cmdL=concat(cmdL,[#,[]]);
  );
  if(makeflg==1,opc="m",opc="");
  ansnm="ans"+nf;
  setdirectory(Dircdy+"/fig");
  tmp=CalcbyM(ansnm,cmdL,[opc]);
  if(tmp!=1,
    ans="error";
  ,
    if(makeflg==1,
      tmp1=replace(fname,"question","mxans");
      setdirectory(Dirdata);
      fid=openfile(tmp1);
      println(fid,parse(ansnm));
      println(fid,"");
      println(fid,"Script");
      apply(cL,println(fid,#));
      println(fid,"end");
      println(fid,"");
      closefile(fid);
      dispL=append(dispL,[tmp1+" created",clrb]);
//      makeflg=0;
    );
  );
  parse(ansnm);
);

Addmxcheck(nf,qfileL,mfileL):=(
  regional(fq,fm,qlineL,mlineL,mline,out,maxL,ansL,
           mxa,mline,ans,lineL,num,cL,
           cmdL,res,flg,tmp,tmp1,tmp2,tmp3);
  fq=fqL_nf;
  fm=fmL_nf;
  if(!isexists(Dirdata,fm),
    dispL=append(dispL,[fm+" not exists",clrr]);
    flg=1;
  ,
    qlineL=Readlines(Dirdata,fq);
    mlineL=Readlines(Dirdata,fm);
    mline=mlineL_1;
    tmp1=mline;
    out=mlineL;
    out=append(out,"");
    tmp=select(1..(length(out)),out_#=="end");
    out=apply(1..(tmp_1),out_#);
    if(substring(mline,0,1)=="[",
      tmp1=substring(mline,1,length(mline)-1);
    );
    tmp=Getlevel(tmp1,",","[]");
    tmp=select(tmp,#_2==0);
    tmp=apply(tmp,#_1);
    forall(tmp,tmp1_#="#");
    maxL=Strsplit(tmp1,"#");
    tmp1=select(1..(length(qlineL)),
             indexof(qlineL_#,"Ans")>0);
    ansL=[];
    forall(tmp1,
      tmp2=#+1;
      tmp=qlineL_tmp2;
      while(substring(tmp,0,1)=="[",
        ansL=append(ansL,[tmp2,tmp]);
        tmp2=tmp2+1;
        tmp=qlineL_tmp2;
      );
    );
    cL=[];
    res="";
    out=concat(out,["","Check"]);
    forall(1..(length(ansL)),nn,
      mxa=maxL_nn;
      mline=append(mline,ansL_nn_2+"   "+mxa);
      tmp1=ansL_nn_2;
      tmp=indexof(tmp1,"]");
      tmp2=substring(tmp1,tmp,length(tmp1));
      ans=Removespace(tmp2);
      tmp3=Getlevel(ans,",","[]");
      tmp3=select(tmp3,#_2==0);
      if(length(tmp3)>0,ans="["+ans+"]");
      num=substring(tmp1,0,tmp);
      flg=0;
      if(length(ans)==0,
        tmp=Dqq("blank");
        flg=1;
      );
      if((flg==0)&(substring(ans,0,1)!=Dq),
        tmp=Errorcheck(ans);
        if(tmp>0,
          tmp==Dqq("???");
          if(tmp==1,tmp=Dqq("?hat?"));
          if(tmp==2,tmp=Dqq("?par?"));
          if(tmp==3,tmp=Dqq("?---?"));
          flg=1;
        );
      );
      if((flg==0)&(substring(mxa,0,1)==Dq),
        tmp=mxa; flg=1;
      );
      if(flg==0,
        tmp1=ans;
        tmp=indexof(tmp1,"=");
        if(tmp>0,
          tmp1=substring(tmp1,tmp,length(tmp1));
        );
        tmp="("+Tomaxform(tmp1)+")  -  ("+mxa+")";
      );
      out=append(out,tmp);
      if(makeflg==1,opc="m",opc="");
      cL=append(cL,"c"+nn+":"+tmp);
      res=res+"c"+nn+"::";
    );
    res=substring(res,0,length(res)-2);
    cL=append(cL,res);
    cmdL=[];
    forall(cL,
      cmdL=concat(cmdL,[#,[]]);
    );
    if(makeflg==1,
      setdirectory(Dircdy+"/fig");
      CalcbyM("ans"+nf,cmdL,[opc]);
      out=append(out,"Result");
      out=append(out,parse("ans"+nf));
      out=append(out,"end");
      setdirectory(Dirdata);
      fid=openfile(fm);
      forall(out,println(fid,#));
      closefile(fid);
    );
  );
//  parse("ans"+nf);
);

